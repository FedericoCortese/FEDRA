# BART Correlation Model with Priors for Guan et al (2020)
data{
 for (k in 1:maxPumps){
   cut[k] = k - 0.5
 }
}
model{
  for (i in 1:nParticipants){
    tmp[i, 1:2] ~ dmnorm(mu, SigmaInv)
    rho[i] = tmp[i, 1]
    psi[i] = tmp[i, 2]
    x[i] ~ dnorm(psi[i], 1/1.5^2)
    beta[i] ~ dnorm(3, 1/2^2)T(0, )
  }
  mu[1] ~ dnorm(7, 1/5^2)T(0,)
  mu[2] ~ dunif(6, 42)
  SigmaInv[1:2, 1:2] ~ dwish(I, 2+1)
  Sigma = inverse(SigmaInv)
  for (i1 in 1:2){
    for (i2 in 1:2){
      r[i1, i2] = Sigma[i1, i2]/sqrt(Sigma[i1, i1]*Sigma[i2, i2])
    }
  }
  for (t in 1:totalTrials){
    yPrime[t] ~ dnorm(rho[participant[t]], 1/beta[participant[t]]^2)T(0, )
    y[t] ~ dinterval(round(yPrime[t]), cut[1:burst[t]])
  }
}
